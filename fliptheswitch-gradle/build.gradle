buildscript {
    dependencies {
        classpath deps.kotlinPlugin
    }
}

apply plugin: 'kotlin'
apply plugin: 'maven'

group = GROUP
version = VERSION_NAME

sourceCompatibility = consts.sourceCompatibility
targetCompatibility = consts.targetCompatibility

// Write the plugin's classpath to a file to share with the tests
task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        def pluginClasspath = sourceSets.main.runtimeClasspath - configurations.provided
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = pluginClasspath.join("\n")
    }
}

dependencies {
    provided gradleApi()
    android deps.androidPlugin
    compile project(':fliptheswitch-core')
    compile deps.javaPoet
    compile deps.kotlinStdLib
    testCompile deps.junit
    testProvided gradleTestKit()

    // Add the classpath file to the test runtime classpath
    testRuntime files(createClasspathManifest)
}

task('copyToExample', type: Copy, dependsOn: 'build') {
    from configurations.compile - configurations.android
    into "../${consts.exampleProjectBuildscriptLibs}"
    from "${buildDir}/libs/fliptheswitch-gradle-${VERSION_NAME}.jar"
    into "../${consts.exampleProjectBuildscriptLibs}"
}
