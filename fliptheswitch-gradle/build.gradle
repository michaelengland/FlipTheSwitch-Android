apply plugin: 'groovy'
apply plugin: 'maven'

group = GROUP
version = VERSION_NAME

sourceCompatibility = consts.sourceCompatibility
targetCompatibility = consts.targetCompatibility

// Write the plugin's classpath to a file to share with the tests
task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
    }
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile project(':fliptheswitch-core')
    compile deps.javaPoet
    compile deps.androidPlugin
    testCompile deps.junit
    testCompile gradleTestKit()

    // Add the classpath file to the test runtime classpath
    testRuntime files(createClasspathManifest)
}

task('copyToExample', type: Copy, dependsOn: 'build') {
    from configurations.compile
    into "../${consts.exampleProjectBuildscriptLibs}"
    from "${buildDir}/libs/fliptheswitch-gradle-${VERSION_NAME}.jar"
    into "../${consts.exampleProjectBuildscriptLibs}"
}
